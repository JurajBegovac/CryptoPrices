plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC7-2"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

detekt {
    version = versions.detekt
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/config/detekt/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt"
        outputName = "detekt-report"
    }
}

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        applicationId "com.releaseit.cryptoprices"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'API_URL', '"https://api.coinmarketcap.com/v1/"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // more lint config on link: https://developer.android.com/studio/write/lint.html#gradle
    lintOptions {
        // Turns off checks for the issue IDs you specify.
        disable 'AllowBackup', 'GoogleAppIndexingWarning', 'UnusedResources'
        // if set to true (default), stops the build if errors are found.
        abortOnError true
        // if true, only report errors.
        ignoreWarnings false

        xmlReport false
        htmlReport true

        htmlOutput = file("$projectDir/build/reports/lint/lint-report.html")
    }

    check.dependsOn 'detektCheck'

    kapt {
        generateStubs = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:' + versions.kotlin

    implementation 'androidx.core:core-ktx:' + versions.androidx
    implementation 'androidx.fragment:fragment-ktx:' + versions.androidx
    implementation 'androidx.appcompat:appcompat:' + versions.androidx
    implementation 'com.google.android.material:material:' + versions.androidx
    implementation 'androidx.recyclerview:recyclerview:' + versions.androidx
    implementation 'androidx.constraintlayout:constraintlayout:' + versions.constraint_layout

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:' + versions.retrofit
    implementation 'com.squareup.retrofit2:converter-gson:' + versions.retrofit
    implementation 'com.squareup.retrofit2:adapter-rxjava2:' + versions.retrofit

    // koin
    implementation 'org.koin:koin-androidx-viewmodel:2.0.1'

    // lifecycle aware components
    implementation 'androidx.lifecycle:lifecycle-livedata:' + versions.archLifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:' + versions.archLifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:' + versions.archLifecycle
    implementation 'androidx.lifecycle:lifecycle-reactivestreams-ktx:' + versions.archLifecycle
    kapt 'androidx.lifecycle:lifecycle-compiler:' + versions.archLifecycle

    // rx java
    implementation 'io.reactivex.rxjava2:rxjava:' + versions.rxjava2
    implementation 'io.reactivex.rxjava2:rxandroid:' + versions.rx_android
    implementation 'io.reactivex.rxjava2:rxkotlin:' + versions.rx_kotlin

    // rx preferences
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:' + versions.rx_preferences

    // rxbinding
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:' + versions.rxbinding
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:' + versions.rxbinding
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:' + versions.rxbinding
    implementation 'com.jakewharton.rxbinding2:rxbinding-design-kotlin:' + versions.rxbinding
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:' + versions.rxbinding

    // rx feedback - rx redux
    implementation 'com.github.NoTests:RxFeedback.kt:0.1.7'

    implementation 'com.github.NoTests.SharedSequence.kt:SharedSequence:0.1.13'

    // testing
    testImplementation 'junit:junit:' + versions.junit
    testImplementation 'io.mockk:mockk:' + versions.mockk
//    testImplementation 'org.mockito:mockito-core:' + versions.mockito
    testImplementation 'androidx.arch.core:core-testing:' + versions.archLifecycle
    androidTestImplementation 'androidx.test.espresso:espresso-core:' + versions.espresso
}
